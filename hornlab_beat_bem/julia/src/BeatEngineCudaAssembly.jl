function assemble_regular_galerkin_operators_cuda_regular(
    mesh::BoundaryMesh{T},
    p1_space::P1Space,
    dp0_space::DP0Space,
    k::T,
    rule::TriangleRule{T};
    skip_singular::Bool=true,
    singular_order::Int=2,
    element_indices=eachindex(mesh.faces),
    cache=nothing,
    return_gpu::Bool=true,
    parallel_quadrature::Bool=true,
    timing=nothing,
    singular_cache=nothing,
    cuda_singular_cache=nothing,
    cuda_image_singular_cache=nothing,
    symmetry_mode::Symbol=:off,
) where {T<:AbstractFloat}
    CUDA.functional() || error("CUDA regular-pair assembly requested, but CUDA.functional() is false.")
    parallel_quadrature || error("Balanced CUDA regular assembly requires parallel_quadrature=true.")
    return_gpu || error("BEAT Engine is CUDA-only; CPU operator materialization has been removed.")

    indices = cache === nothing ? collect(element_indices) : cache.element_indices
    face_count = cache === nothing ? length(mesh.faces) : cache.face_count
    p1_dof_count = p1_space.global_dof_count
    dp0_dof_count = dp0_space.global_dof_count
    rule_count = cache === nothing ? length(rule.points) : cache.rule_count
    total_pairs = length(indices) * length(indices)
    symmetry_images = symmetry_image_transforms(symmetry_mode)
    kernel_mode = "serial_pair_batched"
    kernel_threads = 128
    kernel_blocks = cld(total_pairs, kernel_threads)
    kernel_shmem = 0

    if cache === nothing
        face_vertices, normals, areas, faces, curls = _cuda_geometry_arrays(mesh)
        rule_points, rule_weights = _cuda_rule_arrays(rule)

        _cuda_timed_stage!(timing, "regular_operator_geometry_transfer") do
            d_face_vertices = CuArray(face_vertices)
            d_normals = CuArray(normals)
            d_areas = CuArray(areas)
            d_faces = CuArray(faces)
            d_curls = CuArray(curls)
            d_rule_points = CuArray(rule_points)
            d_rule_weights = CuArray(rule_weights)
            d_test_indices = CuArray(Int32.(indices))
            d_trial_indices = CuArray(Int32.(indices))
            CUDA.synchronize()
            nothing
        end
    else
        timing !== nothing && (timing["regular_operator_geometry_transfer"] = 0.0)
        d_face_vertices = cache.face_vertices
        d_normals = cache.normals
        d_areas = cache.areas
        d_faces = cache.faces
        d_curls = cache.curls
        d_rule_points = cache.rule_points
        d_rule_weights = cache.rule_weights
        d_test_indices = cache.test_indices
        d_trial_indices = cache.trial_indices
    end

    slp_re = slp_im = adj_re = adj_im = dlp_re = dlp_im = hyp_re = hyp_im = nothing
    _cuda_timed_stage!(timing, "regular_operator_gpu_alloc") do
        slp_re = CUDA.zeros(T, p1_dof_count, dp0_dof_count)
        slp_im = CUDA.zeros(T, p1_dof_count, dp0_dof_count)
        adj_re = CUDA.zeros(T, p1_dof_count, dp0_dof_count)
        adj_im = CUDA.zeros(T, p1_dof_count, dp0_dof_count)
        dlp_re = CUDA.zeros(T, p1_dof_count, p1_dof_count)
        dlp_im = CUDA.zeros(T, p1_dof_count, p1_dof_count)
        hyp_re = CUDA.zeros(T, p1_dof_count, p1_dof_count)
        hyp_im = CUDA.zeros(T, p1_dof_count, p1_dof_count)
        CUDA.synchronize()
        nothing
    end

    _cuda_timed_stage!(timing, "regular_operator_kernel") do
        _launch_regular_serial_pair_batched_kernel!(
            slp_re,
            slp_im,
            dlp_re,
            dlp_im,
            adj_re,
            adj_im,
            hyp_re,
            hyp_im,
            d_face_vertices,
            d_normals,
            d_areas,
            d_faces,
            d_curls,
            d_test_indices,
            d_trial_indices,
            d_rule_points,
            d_rule_weights,
            k,
            p1_dof_count,
            face_count,
            rule_count,
            total_pairs,
        )
        for transform in symmetry_images
            _launch_regular_symmetry_image_kernel!(
                slp_re,
                slp_im,
                dlp_re,
                dlp_im,
                adj_re,
                adj_im,
                hyp_re,
                hyp_im,
                d_face_vertices,
                d_normals,
                d_areas,
                d_faces,
                d_curls,
                d_test_indices,
                d_trial_indices,
                d_rule_points,
                d_rule_weights,
                k,
                p1_dof_count,
                dp0_dof_count,
                face_count,
                rule_count,
                total_pairs,
                128,
                transform,
            )
        end
        CUDA.synchronize()
        nothing
    end

    correction_cache = singular_cache
    adjacent_pairs = _cuda_timed_stage!(timing, "regular_operator_count_transfer") do
        if correction_cache === nothing
            count_adjacent_pairs(mesh, indices)
        else
            correction_cache.pair_count
        end
    end
    regular_pairs = total_pairs - adjacent_pairs + length(symmetry_images) * total_pairs

    single_layer = double_layer = adjoint_double_layer = hypersingular = nothing
    _cuda_timed_stage!(timing, "regular_operator_complex_materialize") do
        single_layer = _complex_gpu_matrix(slp_re, slp_im)
        double_layer = _complex_gpu_matrix(dlp_re, dlp_im)
        adjoint_double_layer = _complex_gpu_matrix(adj_re, adj_im)
        hypersingular = _complex_gpu_matrix(hyp_re, hyp_im)
        CUDA.synchronize()
        nothing
    end
    timing !== nothing && (timing["regular_operator_cpu_transfer"] = 0.0)

    if skip_singular
        singular_pairs = 0
        skipped_pairs = adjacent_pairs
    else
        correction_cache === nothing && (correction_cache = build_singular_correction_cache(mesh, singular_order, indices))
        singular_pairs = add_singular_corrections_cuda_compact!(
            (
                single_layer=single_layer,
                double_layer=double_layer,
                adjoint_double_layer=adjoint_double_layer,
                hypersingular=hypersingular,
            ),
            mesh,
            p1_space,
            dp0_space,
            k,
            singular_order,
            indices,
            correction_cache,
            cuda_singular_cache=cuda_singular_cache,
            cuda_regular_cache=cache,
            timing=timing,
        )
        skipped_pairs = 0
    end

    image_singular_pairs = _cuda_timed_stage!(timing, "regular_operator_image_singular_corrections") do
        if skip_singular || isempty(symmetry_images)
            0
        else
            add_image_singular_corrections_cuda_compact!(
                (
                    single_layer=single_layer,
                    double_layer=double_layer,
                    adjoint_double_layer=adjoint_double_layer,
                    hypersingular=hypersingular,
                    on_gpu=true,
                ),
                mesh,
                p1_space,
                dp0_space,
                k,
                rule,
                singular_order,
                indices,
                symmetry_mode;
                cuda_regular_cache=cache,
                cuda_image_singular_cache=cuda_image_singular_cache,
                timing=timing,
            )
        end
    end

    _cuda_timed_stage!(timing, "regular_operator_symmetry_row_weights") do
        _apply_operator_p1_row_weights!(
            (
                single_layer=single_layer,
                double_layer=double_layer,
                adjoint_double_layer=adjoint_double_layer,
                hypersingular=hypersingular,
                on_gpu=true,
            ),
            mesh,
            symmetry_mode,
        )
    end

    return (
        single_layer=single_layer,
        double_layer=double_layer,
        adjoint_double_layer=adjoint_double_layer,
        hypersingular=hypersingular,
        regular_pairs=regular_pairs,
        singular_pairs=singular_pairs,
        skipped_pairs=skipped_pairs,
        image_singular_pairs=image_singular_pairs,
        on_gpu=true,
        regular_kernel_threads=kernel_threads,
        regular_kernel_blocks=kernel_blocks,
        regular_kernel_shared_memory_bytes=kernel_shmem,
        regular_kernel_qpair_count=rule_count * rule_count,
        regular_kernel_total_pairs=total_pairs,
        regular_kernel_mode=kernel_mode,
        regular_assembly_mode=:serial_pair_batched,
    )
end
